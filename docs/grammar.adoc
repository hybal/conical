= Grammar for Conical 
:hardbreaks:

[NOTE]
Everything in here is subject to change

.Key
====

`+` is one or more
`*` is zero or more
`?` is zero or one
`|` is either or
`[]` is a character class that can include ranges of characters
`{?}` is any character 
`{expr, count}` is repeat expression for 0-to-count times
`{expr, start, end}` is repeat expression for start-to-end times (inclusive)
`"a"` is a literal

====

[source, ebnf]
.Grammar
----

<expression> ::= <assignment>

<assignment> ::= <ternary>
               | <unary> "=" <assignment>
               | <unary> "+=" <assignment>
               | <unary> "-=" <assignment>
               | <unary> "*=" <assignment>
               | <unary> "/=" <assignment>
               | <unary> "%=" <assignment>
               | <unary> "<<=" <assignment>
               | <unary> ">>=" <assignment>
               | <unary> "&=" <assignment>
               | <unary> "^=" <assignment>
               | <unary> "|=" <assignment>

<ternary> ::= <logical_or>
            | <logical_or> "?" <expression> ":" <ternary>

<logical_or> ::= <logical_and>
               | <logical_or> "||" <logical_and>

<logical_and> ::= <bitwise_or>
                | <logical_and> "&&" <bitwise_or>

<bitwise_or> ::= <bitwise_xor>
               | <bitwise_or> "|" <bitwise_xor>

<bitwise_xor> ::= <bitwise_and>
                | <bitwise_xor> "^" <bitwise_and>

<bitwise_and> ::= <equality>
                | <bitwise_and> "&" <equality>

<equality> ::= <relational>
             | <equality> "==" <relational>
             | <equality> "!=" <relational>

<relational> ::= <shift>
               | <relational> "<" <shift>
               | <relational> "<=" <shift>
               | <relational> ">" <shift>
               | <relational> ">=" <shift>

<shift> ::= <additive>
          | <shift> "<<" <additive>
          | <shift> ">>" <additive>

<additive> ::= <multiplicative>
             | <additive> "+" <multiplicative>
             | <additive> "-" <multiplicative>

<multiplicative> ::= <unary>
                   | <multiplicative> "*" <unary>
                   | <multiplicative> "/" <unary>
                   | <multiplicative> "%" <unary>

<unary> ::= <postfix>
          | "+" <unary>
          | "-" <unary>
          | "!" <unary>
          | "~" <unary>
          | "*" <unary>
          | "&" <unary>
          | "sizeof" <unary>
          | "(" <type> ")" <unary>    (* Type cast *)

<postfix> ::= <primary>
            | <postfix> "[" <expression> "]"   (* Array indexing *)
            | <postfix> "(" <argument_list> ")" (* Function call *)
            | <postfix> "." <identifier>       (* Struct member access *)
            | <postfix> "->" <identifier>      (* Pointer member access *)

<primary> ::= <identifier>
            | <literal>
            | "(" <expression> ")"

<argument_list> ::= <expression>
                 | <expression> "," <argument_list>
----
